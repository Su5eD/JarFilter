plugins {
    id 'java'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'dev.su5ed'
version = '1.0.0'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

configurations {
    shade {}
    
    implementation.extendsFrom shade
}

license {
    header file('NOTICE')
    newLine false
}

repositories {
    mavenCentral()
}

dependencies {
    shade 'net.sf.jopt-simple:jopt-simple:5.0.4'
}

tasks.withType(Jar) {
    manifest {
        attributes([
                'Specification-Title': project.name,
                'Specification-Vendor': 'Su5eD',
                'Specification-Version': '1.0.0',
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Su5eD',
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Main-Class': 'dev.su5ed.jarfilter.JarFilter'
        ])
    }
}

shadowJar {
    configurations = [project.configurations.shade]
    archiveClassifier = 'shaded'
}

assemble {
    dependsOn shadowJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url 'https://maven.su5ed.dev/releases'
            credentials {
                username = System.env.MAVEN_USER ?: 'not'
                password = System.env.MAVEN_PASSWORD ?: 'set'
            }
        }
    }
}